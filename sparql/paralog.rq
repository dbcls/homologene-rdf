# Get paralogs for a given NCBI Gene ID

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX orth: <http://purl.org/net/orth#>
PREFIX ncbigene: <http://identifiers.org/ncbigene/>

SELECT DISTINCT ?taxon ?paralog ?paralog_label
WHERE {
  VALUES ?query { ncbigene:276 }
  ?query obo:RO_0002162 ?taxon .
  ?group orth:inDataset <https://ncbi.nlm.nih.gov/homologene/> ;
      orth:hasHomologousMember ?query , ?paralog .
  ?paralog obo:RO_0002162 ?taxon ;
      rdfs:label ?paralog_label .
  FILTER (?query != ?paralog)
}
ORDER BY ?taxon ?paralog_label
